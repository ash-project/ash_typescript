# SPDX-FileCopyrightText: 2025 ash_typescript contributors <https://github.com/ash-project/ash_typescript/graphs/contributors>
#
# SPDX-License-Identifier: MIT

defmodule AshTypescript.TypedController.Codegen do
  @moduledoc """
  Entry point for TypeScript path helper code generation.

  Orchestrates the generation of TypeScript path helper functions
  from typed controller routes configured in the DSL.
  """

  alias AshTypescript.TypedController.Codegen.{
    RouteConfigCollector,
    RouteRenderer,
    RouterIntrospector
  }

  @doc """
  Generates TypeScript code for all configured typed controller routes.

  ## Parameters
  - `opts` - Options including `:router` for the Phoenix router module

  ## Returns
  A string containing the generated TypeScript code.
  """
  def generate(opts \\ []) do
    router =
      Keyword.get(opts, :router) ||
        AshTypescript.router()

    routes_config = RouteConfigCollector.get_typed_controllers()

    if routes_config == [] do
      ""
    else
      route_infos =
        if router do
          Code.ensure_loaded(router)

          if function_exported?(router, :__routes__, 0) do
            RouterIntrospector.introspect(router, routes_config)
          else
            build_route_infos_without_router(routes_config)
          end
        else
          build_route_infos_without_router(routes_config)
        end

      validate_path_param_arguments!(route_infos)
      generate_typescript(route_infos)
    end
  end

  @doc false
  def validate_path_param_arguments!(route_infos) do
    Enum.each(route_infos, fn route_info ->
      %{route: route, path: path, path_params: path_params} = route_info

      arg_names = MapSet.new(route.arguments, & &1.name)

      missing =
        Enum.reject(path_params, fn param ->
          MapSet.member?(arg_names, param)
        end)

      if missing != [] do
        missing_str =
          Enum.map_join(missing, ", ", fn param ->
            ":#{param}"
          end)

        suggestions =
          Enum.map_join(missing, "\n", fn param ->
            "    argument :#{param}, :string"
          end)

        raise """
        Route :#{route.name} has path "#{path}" with path parameters #{missing_str} \
        that don't have matching DSL arguments.

        Add the missing arguments to the route definition:

        route :#{route.name} do
        #{suggestions}
        end
        """
      end
    end)
  end

  defp build_route_infos_without_router(routes_config) do
    Enum.flat_map(routes_config, fn {source_module, controller_module, routes} ->
      Enum.map(routes, fn route ->
        %{
          source_module: source_module,
          controller: controller_module,
          route: route,
          path: nil,
          method: route.method,
          path_params: [],
          scope_prefix: nil
        }
      end)
    end)
  end

  defp generate_typescript(route_infos) do
    header = """
    // This file is auto-generated by AshTypescript. Do not edit manually.

    """

    sorted_infos =
      Enum.sort_by(route_infos, fn info ->
        {info.scope_prefix || "", info.route.name}
      end)

    functions = Enum.map_join(sorted_infos, "\n", &RouteRenderer.render/1)

    header <> functions
  end
end
