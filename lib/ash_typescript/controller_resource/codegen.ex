# SPDX-FileCopyrightText: 2025 ash_typescript contributors <https://github.com/ash-project/ash_typescript/graphs/contributors>
#
# SPDX-License-Identifier: MIT

defmodule AshTypescript.ControllerResource.Codegen do
  @moduledoc """
  Entry point for TypeScript path helper code generation.

  Orchestrates the generation of TypeScript path helper functions
  from controller routes configured in the DSL.
  """

  alias AshTypescript.ControllerResource.Codegen.{
    RouteConfigCollector,
    RouteRenderer,
    RouterIntrospector
  }

  @doc """
  Generates TypeScript code for all configured controller resource routes.

  ## Parameters
  - `otp_app` - The OTP application atom
  - `opts` - Options including `:router` for the Phoenix router module

  ## Returns
  A string containing the generated TypeScript code.
  """
  def generate(otp_app, opts \\ []) do
    router =
      Keyword.get(opts, :router) ||
        AshTypescript.router()

    routes_config = RouteConfigCollector.get_controller_resources(otp_app)

    if routes_config == [] do
      ""
    else
      route_infos =
        if router do
          Code.ensure_loaded(router)

          if function_exported?(router, :__routes__, 0) do
            RouterIntrospector.introspect(router, routes_config)
          else
            build_route_infos_without_router(routes_config)
          end
        else
          build_route_infos_without_router(routes_config)
        end

      generate_typescript(route_infos)
    end
  end

  defp build_route_infos_without_router(routes_config) do
    Enum.flat_map(routes_config, fn {resource, controller_module, routes} ->
      Enum.map(routes, fn route ->
        %{
          resource: resource,
          controller: controller_module,
          route_action: route,
          path: nil,
          method: route.method,
          path_params: [],
          scope_prefix: nil
        }
      end)
    end)
  end

  defp generate_typescript(route_infos) do
    header = """
    // This file is auto-generated by AshTypescript. Do not edit manually.

    """

    functions = Enum.map_join(route_infos, "\n", &RouteRenderer.render/1)

    header <> functions
  end
end
