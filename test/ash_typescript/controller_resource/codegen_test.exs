# SPDX-FileCopyrightText: 2025 ash_typescript contributors <https://github.com/ash-project/ash_typescript/graphs/contributors>
#
# SPDX-License-Identifier: MIT

defmodule AshTypescript.ControllerResource.CodegenTest do
  use ExUnit.Case

  @moduletag :ash_typescript

  setup_all do
    typescript =
      AshTypescript.ControllerResource.Codegen.generate(:ash_typescript,
        router: AshTypescript.Test.ControllerResourceTestRouter
      )

    %{typescript: typescript}
  end

  describe "multi-mount code generation" do
    setup do
      typescript =
        AshTypescript.ControllerResource.Codegen.generate(:ash_typescript,
          router: AshTypescript.Test.ControllerResourceMultiMountRouter
        )

      %{typescript: typescript}
    end

    test "generates prefixed path helpers for each scope", %{typescript: typescript} do
      assert String.contains?(typescript, "export function adminAuthPath(")
      assert String.contains?(typescript, "export function appAuthPath(")
    end

    test "generates prefixed action functions for each scope", %{typescript: typescript} do
      assert String.contains?(typescript, "export async function adminLogin(")
      assert String.contains?(typescript, "export async function appLogin(")
    end

    test "generates prefixed input types for each scope", %{typescript: typescript} do
      assert String.contains?(typescript, "export type AdminLoginInput = {")
      assert String.contains?(typescript, "export type AppLoginInput = {")
    end

    test "generates prefixed PATCH functions for each scope", %{typescript: typescript} do
      assert String.contains?(typescript, "export async function adminUpdateProvider(")
      assert String.contains?(typescript, "export async function appUpdateProvider(")
    end

    test "generates prefixed input types for PATCH routes", %{typescript: typescript} do
      assert String.contains?(typescript, "export type AdminUpdateProviderInput = {")
      assert String.contains?(typescript, "export type AppUpdateProviderInput = {")
    end

    test "does not generate unprefixed helpers when multi-mounted", %{typescript: typescript} do
      refute Regex.match?(~r/function authPath\(/, typescript)
      refute Regex.match?(~r/function login\(/, typescript)
    end
  end

  describe "ambiguous mount error" do
    test "raises when router has duplicate mounts without as: options" do
      assert_raise RuntimeError, ~r/don't have unique `as:` options/, fn ->
        AshTypescript.ControllerResource.Codegen.generate(:ash_typescript,
          router: AshTypescript.Test.ControllerResourceAmbiguousRouter
        )
      end
    end
  end

  describe "GET route path helpers" do
    test "generates non-empty TypeScript output", %{typescript: typescript} do
      assert typescript != ""
      assert String.contains?(typescript, "// This file is auto-generated by AshTypescript")
    end

    test "generates path helper for auth route", %{typescript: typescript} do
      assert String.contains?(typescript, "export function authPath(")
      assert String.contains?(typescript, "return \"/auth\"")
    end

    test "generates path helper for provider_page route with path param", %{
      typescript: typescript
    } do
      assert String.contains?(typescript, "export function providerPagePath(")
      assert String.contains?(typescript, "provider: string")
    end

    test "GET routes do not generate async functions", %{typescript: typescript} do
      [get_section | _] = String.split(typescript, "export type LoginInput")

      refute String.contains?(get_section, "async function")
      refute String.contains?(get_section, "fetch(")
      refute String.contains?(get_section, "Promise<")
    end

    test "generates JSDoc for path helpers", %{typescript: typescript} do
      assert String.contains?(typescript, "Path helper for /auth")
      assert String.contains?(typescript, "Path helper for /auth/providers/:provider")
    end
  end

  describe "POST route action functions" do
    test "generates typed input type for login action", %{typescript: typescript} do
      assert String.contains?(typescript, "export type LoginInput = {")
      assert String.contains?(typescript, "code: string;")
      assert String.contains?(typescript, "rememberMe?: boolean;")
    end

    test "generates async function for login with input param", %{typescript: typescript} do
      assert String.contains?(typescript, "export async function login(")
      assert String.contains?(typescript, "input: LoginInput")
      assert String.contains?(typescript, "Promise<Response>")
    end

    test "login function sends POST with JSON body", %{typescript: typescript} do
      assert String.contains?(typescript, "method: \"POST\"")
      assert String.contains?(typescript, "body: JSON.stringify(input)")
    end

    test "generates async function for logout without input param", %{typescript: typescript} do
      assert String.contains?(typescript, "export async function logout(")
      assert String.contains?(typescript, "config?: {")
      refute String.contains?(typescript, "LogoutInput")
    end

    test "logout function sends POST without body", %{typescript: typescript} do
      [_, after_logout] = String.split(typescript, "export async function logout(", parts: 2)
      [logout_body | _] = String.split(after_logout, "\n}\n", parts: 2)
      refute String.contains?(logout_body, "body:")
    end

    test "action functions accept optional config with headers", %{typescript: typescript} do
      assert String.contains?(typescript, "config?: { headers?: Record<string, string> }")
    end

    test "generates JSDoc for action functions", %{typescript: typescript} do
      assert String.contains?(typescript, "POST /auth/login")
      assert String.contains?(typescript, "POST /auth/logout")
    end
  end

  describe "PATCH route with path params and input" do
    test "generates typed input type for update_provider action", %{typescript: typescript} do
      assert String.contains?(typescript, "export type UpdateProviderInput = {")
      assert String.contains?(typescript, "enabled: boolean;")
      assert String.contains?(typescript, "displayName?: string;")
    end

    test "generates async function with path and input params", %{typescript: typescript} do
      assert String.contains?(typescript, "export async function updateProvider(")
      assert String.contains?(typescript, "path: { provider: string }")
      assert String.contains?(typescript, "input: UpdateProviderInput")
      assert String.contains?(typescript, "Promise<Response>")
    end

    test "update_provider function sends PATCH with JSON body", %{typescript: typescript} do
      assert String.contains?(typescript, "method: \"PATCH\"")
    end

    test "update_provider function uses path object for URL template", %{typescript: typescript} do
      assert String.contains?(typescript, "${path.provider}")
    end

    test "generates JSDoc for PATCH action", %{typescript: typescript} do
      assert String.contains?(typescript, "PATCH /auth/providers/:provider")
    end
  end
end
